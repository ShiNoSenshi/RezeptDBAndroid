apply plugin: 'com.android.application'

def appPropertiesFile = rootProject.file("app-settings.properties")
def appProperties = new Properties()
appProperties.load(new FileInputStream(appPropertiesFile))

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "at.cooperation.rezeptdb"
        minSdkVersion 24
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
            manifestPlaceholders = [appName: "@string/app_name"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def environmentPath = appProperties["env.path.test"]
            def envPropertiesFile = rootProject.file(environmentPath)
            def envProperties = new Properties()
            envProperties.load(new FileInputStream(envPropertiesFile))
            for (String key : envProperties.keySet()) {
                buildConfigField "String", key.replaceAll("\\.", "_").toUpperCase(), envProperties[key]

            }
        }
        release {
            minifyEnabled false
            multiDexEnabled true
            manifestPlaceholders = [appName: "@string/app_name"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            def environmentPath = appProperties["env.path.live"]
            def envPropertiesFile = rootProject.file(environmentPath)
            def envProperties = new Properties()
            envProperties.load(new FileInputStream(envPropertiesFile))
            for (String key : envProperties.keySet()) {
                buildConfigField "String", key.replaceAll("\\.", "_").toUpperCase(), envProperties[key]

            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:support-annotations:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    api 'com.android.volley:volley:1.1.1'
}
